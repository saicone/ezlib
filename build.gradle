plugins {
    id 'com.gradleup.shadow' version '8.3.5' apply false
    id 'net.kyori.blossom' version '1.3.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs.add('-XDenableSunApiLintControl')
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withSourcesJar()
        withJavadocJar()
    }
}

blossom {
    replaceTokenIn('src/main/java/com/saicone/ezlib/Ezlib.java')
    replaceToken '${version}', System.getenv("VERSION") ?: project.version
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
        }
    }
}

def javadocProjects= [
        ':',
        ':annotations',
        ':loader'
]

tasks.register('allJavadoc', Javadoc) {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
    destinationDir = file(project.layout.buildDirectory.dir('docs/all-javadoc'))
}